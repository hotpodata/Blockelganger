apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "true"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "false"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "false"
        }
    }

    signingConfigs {
        def provDir = System.getenv("BLOCKELGANGER_KEYSTORE_DIRECTORY") ?: ""
        def blockelgangerKeystorePass = System.getenv("BLOCKELGANGER_KEYSTORE_PASS") ?: ""
        def blockelgangerProPass = System.getenv("BLOCKELGANGER_PRO_PASS") ?: ""
        blockelgangerProRelease {
            storeFile file("${provDir}/blockelganger.keystore")
            storePassword blockelgangerKeystorePass
            keyAlias "blockelganger-pro-release"
            keyPassword blockelgangerProPass
        }

        def blockelgangerFreePass = System.getenv("BLOCKELGANGER_FREE_PASS") ?: ""
        blockelgangerFreeRelease {
            storeFile file("${provDir}/blockelganger.keystore")
            storePassword blockelgangerKeystorePass
            keyAlias "blockelganger-release"
            keyPassword blockelgangerFreePass
        }
    }
    productFlavors {
        def freeSigning = signingConfigs.blockelgangerFreeRelease
        def proSigning = signingConfigs.blockelgangerProRelease
        Free {
            applicationId "com.hotpodata.blockelganger"
            buildConfigField "boolean", "IS_PRO", "false"
            signingConfig freeSigning
            resValue "string", "app_label", "Blockelganger"
        }
        Pro {
            applicationId "com.hotpodata.blockelganger.pro"
            buildConfigField "boolean", "IS_PRO", "true"
            signingConfig proSigning
            resValue "string", "app_label", "Blockelganger Pro"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':BlockLib')
    compile project(':common')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.google.android.gms:play-services-games:8.3.0'
    compile 'com.google.android.gms:play-services-ads:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxkotlin:0.30.1'
    compile 'io.reactivex:rxjava:1.0.16'
    compile 'me.grantland:autofittextview:0.2.+'
}
task switchToFree(type: Copy) {
    description = 'Switches to FREE google-services.json'
    from "src/free"
    include "google-services.json"
    into "."
}

task switchToPro(type: Copy) {
    description = 'Switches to PRO google-services.json'
    from "src/pro"
    include "google-services.json"
    into "."
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-4589'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
afterEvaluate {
    processFreeDebugGoogleServices.dependsOn switchToFree
    processFreeReleaseGoogleServices.dependsOn switchToFree
    processProDebugGoogleServices.dependsOn switchToPro
    processProReleaseGoogleServices.dependsOn switchToPro
}

